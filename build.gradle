plugins { id "org.ajoberstar.grgit" version "3.0.0-beta.1" }

apply plugin: "java"
apply plugin: "application"
apply plugin: "idea"

version = '1.0-alpha26'
group = "com.github.acasteigts"
sourceCompatibility = 1.8 // java 8
targetCompatibility = 1.8
applicationName = "jbotsimAll"
mainClassName = "examples.basic.helloworld.Main"
description = "JBotSim : a simulation library for distributed algorithms in dynamic networks."


repositories {
    mavenCentral()
}

/**
 * Clone GIT repo of Graphviz-Java
 */
task cloneGraphvizJava {
    project.ext.dotparserDir = buildDir.toString() +"/dotparser"
    if (! file(project.ext.dotparserDir).exists ()) {
        project.ext.dotParserRepo = grgit.clone {
            dir = project.ext.dotparserDir
            uri = "https://github.com/nidi3/graphviz-java.git"
            refToCheckout = "master"
        }
    }
}

task copyDotParserToSrc (type: Copy) {
    def baseDir = project.ext.dotparserDir.toString()+ "/graphviz-java/src/main/java"
    def dstDir = "src/"

    from (baseDir) {
        exclude "**/engine"
        exclude "**/service"
    }
    into (dstDir)

    dependsOn cloneGraphvizJava
    enabled = ! file(dstDir+"/guru").exists()
}

compileJava.dependsOn copyDotParserToSrc

task cleanDotParserSources(type: Delete) {
    delete "src/guru"
}

clean.dependsOn cleanDotParserSources

sourceSets {
    main {
        java {
            srcDirs = [ "src/guru", "src/jbotsim", "src/jbotsimx", "src/examples" ]
        }
        resources {
            srcDirs = main.java.srcDirs
        }
    }

    jbotsimCore {
        java {
            srcDirs = main.java.srcDirs
            include "jbotsim/**/*.java"
        }
        resources {
            srcDirs = main.resources.srcDirs
            include "jbotsim/**/*"
        }
    }

    jbotsim {
        java {
            srcDirs = main.java.srcDirs
            exclude "examples/**/*.java"
        }
        resources {
            srcDirs = main.resources.srcDirs
            exclude "examples/**/*"
        }
    }

    test {
        java {
            srcDirs = [ "test/java"]
        }
        resources {
            srcDirs = [ "test/resources" ]
        }
    }
}
// to prevent errors with french comments
compileJava.options.encoding = 'UTF-8'
compileJbotsimJava.options.encoding = 'UTF-8'
compileJbotsimCoreJava.options.encoding = 'UTF-8'

task jbotsimCoreJar(type: Jar){
    baseName = "jbotsim-core"
	from sourceSets.jbotsimCore.output
}

task jbotsimJar(type: Jar){
	baseName = "jbotsim"
	from sourceSets.jbotsim.output
}

jar{
	baseName = "jbotsim-all"
	from sourceSets.main.output
}

jar.finalizedBy jbotsimCoreJar
jar.finalizedBy jbotsimJar

task jbotsimCoreDoc(type: Javadoc) {
  source = sourceSets.jbotsimCore.allJava
  destinationDir = file("$buildDir/docs/jbotsim-core")
}

task jbotsimDoc(type: Javadoc) {
  source = sourceSets.jbotsim.allJava
  destinationDir = file("$buildDir/docs/jbotsim")
}

javadoc.finalizedBy jbotsimCoreDoc
javadoc.finalizedBy jbotsimDoc

//Create a binary for each example :

task createAllStartScripts() << {
     // just a placeholder
}
  def scripts = [:]
  fileTree("${project.rootDir}" + File.separator + "src" + File.separator + "examples" + File.separator + "basic").visit{
    details -> if(details.isDirectory()){
      scripts.put(details.file.getName(), "examples.basic." + details.file.getName() + ".Main")
    }
  }
  scripts.each() { scriptName, className ->
          def t = tasks.create(name: scriptName+'StartScript', type: CreateStartScripts) {
        mainClassName = className
        applicationName = scriptName
        outputDir = new File(project.buildDir, 'scripts')
        classpath = jar.outputs.files + project.configurations.runtime
    }
    createAllStartScripts.dependsOn(t)
  }

/**
 * Tests
 */

// Non-standard location of tests
sourceSets {
    test {
    }

    jbotsimJarTest {
        java.srcDirs = test.java.srcDirs
        resources.srcDirs = test.resources.srcDirs
        compileClasspath += jbotsimJar.outputs.files
        runtimeClasspath += jbotsimJar.outputs.files
    }

}

dependencies {
    testImplementation 'junit:junit:4.12'
    jbotsimJarTestImplementation 'junit:junit:4.12'
}

configurations {
    jbotsimJarTestImplementation.extendsFrom implementation
    jbotsimJarTestRuntimeOnly.extendsFrom runtimeOnly
}

def commonTestSettings = {
    useJUnit ()
    testLogging {
        warn {
            events = [ "passed", "failed", "skipped" ]
            showCauses = true
            showStackTraces = true
            showExceptions = true
            exceptionFormat = "full"
        }
    }

    filter {
        includeTestsMatching "*"
    }

    doLast {
        String outDirName = rootDir.toPath().relativize(project.testReportDir.toPath()).toString()
        println ()
        println ("View test report in: " + outDirName +  File.separator + name + File.separator + "index.html")
    }
}

test {
    configure commonTestSettings
}

task jbotsimJarTest (type : Test) {
    configure commonTestSettings
    testClassesDirs = sourceSets.jbotsimJarTest.output.classesDirs
    classpath = sourceSets.jbotsimJarTest.runtimeClasspath
    shouldRunAfter test

    println(classpath.files)
}


check.dependsOn jbotsimJarTest
